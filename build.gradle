plugins {
	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'java'
	id 'war'
}

ext {
	file_name_build = 'rainments-wss-build.war'
	file_name_prod = 'rainments-wss.war'
	file_name_int = 'rainments-int-wss.war'
}

bootWar {
	archiveFileName = file_name_build
}

processResources {
	filesMatching('gr.properties') {
		expand(project.properties)
	}
}

group = 'com.romanpulov'
version = '0.4.8'

repositories {
	mavenCentral()
	mavenLocal()
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	//dialects to support SQLite
	implementation 'org.hibernate.orm:hibernate-community-dialects'

	implementation 'org.xerial:sqlite-jdbc:3.46.0.0'
	implementation 'org.apache.poi:poi:5.3.0'

	implementation 'com.romanpulov:jutils-core:0.4.4'

	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

//configurations {
//	runtime.exclude module: 'spring-boot-starter-tomcat'
//}

test {
	useJUnitPlatform()
}

apply from: '../common/gradle/tomcat.config.gradle'

tasks.register('deployInt', Copy) {
	dependsOn(bootWar)
	from "build/libs"
	into tomcat_webapps
	include file_name_build
	rename file_name_build, file_name_int
}

tasks.register('deployProd', Copy) {
	dependsOn(bootWar)
	from "build/libs"
	into tomcat_webapps
	include file_name_build
	rename file_name_build, file_name_prod
}

tasks.register('printProps') {
	doLast {
		println tomcat_webapps
	}
}
